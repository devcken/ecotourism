plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'org.flywaydb.flyway' version '5.2.4'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.kakaopay'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    querydsl {
        extendsFrom compileClasspath
    }
}

repositories {
    jcenter()
}

ext {
    spockVersion = '1.3-groovy-2.5'
    snippetsDir = file('build/generated-snippets')
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/querydsl/java']
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:2.0.0.RELEASE'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation'com.querydsl:querydsl-jpa'
    implementation 'com.querydsl:querydsl-apt'
    annotationProcessor 'com.querydsl:querydsl-apt'
    runtime 'mysql:mysql-connector-java'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtime 'org.codehaus.groovy:groovy'

    implementation 'com.opencsv:opencsv:4.5'

    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.flywaydb:flyway-core'
}

compileJava {
    dependsOn processResources
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}

querydsl {
    jpa = true
}

test {
    outputs.dir snippetsDir
    useJUnit {
        excludeCategories 'com.kakaopay.ecotourism.spec.ApiDocumentationSpec'
    }
}

task integrationTest(type: Test, group: 'verification') {
    useJUnit {
        includeCategories 'com.kakaopay.ecotourism.spec.ApiDocumentationSpec'
    }
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

flyway {
    driver = 'com.mysql.cj.jdbc.Driver'
    url = flywayUrl
    user = flywayUsername
    password = flywayPassword
    locations = ['classpath:db/migration', 'classpath:db/data']
}
