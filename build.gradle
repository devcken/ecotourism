plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'org.flywaydb.flyway' version '5.2.4'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'io.spring.dependency-management'

group = 'com.kakaopay'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    jcenter()
}

ext {
    spockVersion = '1.3-groovy-2.5'
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtime 'mysql:mysql-connector-java'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtime 'org.codehaus.groovy:groovy'

    implementation 'com.opencsv:opencsv:4.5'

    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.flywaydb:flyway-core'
}

compileJava.dependsOn(processResources)

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

test {
    outputs.dir snippetsDir
    useJUnit {
        excludeCategories 'com.kakaopay.ecotourism.ApiDocumentationSpec'
    }
}

task integrationTest(type: Test, group: 'verification') {
    useJUnit {
        includeCategories 'com.kakaopay.ecotourism.ApiDocumentationSpec'
    }
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

flyway {
    driver = 'com.mysql.cj.jdbc.Driver'
    url = flywayUrl
    user = flywayUsername
    password = flywayPassword
}
